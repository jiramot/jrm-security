buildscript {
    ext {
        kotlinVersion = '1.3.10'
        springBootVersion = '2.1.1.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")
        classpath("org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}")
    }
}

apply plugin: 'maven'
apply plugin: 'kotlin'
apply plugin: 'kotlin-spring'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'signing'

group = 'com.github.jiramot'
archivesBaseName = "security"
version = "0.0.1"


signing {
    sign configurations.archives
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}
task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}
artifacts {
    archives javadocJar, sourcesJar
}

sourceCompatibility = 1.8
compileKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = "1.8"
    }
}

repositories {
    mavenCentral()
}

ext['springCloudVersion'] = 'Finchley.RELEASE'

dependencies {
    implementation('org.springframework.boot:spring-boot-starter-web')
    implementation('org.springframework.boot:spring-boot-starter-security')
    implementation('com.nimbusds:nimbus-jose-jwt:6.0')
    implementation('commons-io:commons-io:2.6')
    implementation('io.jsonwebtoken:jjwt-api:0.10.5')
    implementation('io.jsonwebtoken:jjwt-impl:0.10.5')
    implementation('org.bouncycastle:bcprov-jdk15on:1.60')
    implementation('io.jsonwebtoken:jjwt-jackson:0.10.5')
    testImplementation("org.junit.jupiter:junit-jupiter-api:5.2.0")
    testImplementation("org.junit.jupiter:junit-jupiter-engine:5.2.0")
    testImplementation('org.springframework.security:spring-security-test')
    testImplementation group: 'org.hamcrest', name: 'hamcrest-library', version: '1.3'
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

bootJar.enabled = false
jar.enabled = true

uploadArchives {
    repositories {
        mavenDeployer {

            // Sign POM
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            // Destination
            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }
            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            // Add required metadata to POM
            pom.project {
                name 'JRM Security'
                packaging 'jar'
                description 'Spring security extension with JWT'
                url 'https://github.com/jiramot/jrm-security'

                scm {
                    connection 'scm:git:git://github.com/jiramot/jrm-security.git'
                        developerConnection 'scm:git:ssh://github.com/jiramot/jrm-security.git'
                        url 'https://github.com/jiramot/jrm-security'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'jiramot'
                        name 'Watchanon Numnam'
                        email 'jiramot@gmail.com'
                    }
                }
            }
        }
    }
}
